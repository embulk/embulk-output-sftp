plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id "com.github.kt3k.coveralls" version "2.4.0"
    id "jacoco"
    id "java"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.3.0"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.output.sftp"
version = "0.2.6"
description = "Stores files on SFTP server."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile "org.apache.commons:commons-vfs2:2.2"
    compile "commons-io:commons-io:2.6"
    compile "com.github.mwiede:jsch:0.1.60"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
    testCompile "org.apache.sshd:apache-sshd:1.1.0+"
    testCompile "org.littleshoot:littleproxy:1.1.0-beta1"
    testCompile "io.netty:netty-all:4.0.34.Final"
    testCompile "org.mockito:mockito-core:2.28.2"
}

embulkPlugin {
    mainClass = "org.embulk.output.sftp.SftpFileOutputPlugin"
    category = "output"
    type = "sftp"
}

// add tests/javadoc/source jar tasks as artifacts to be released
task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

// bintray
bintray {
    // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    publications = [ "embulkMavenRelease" ]
    publish = true

    pkg {
        userOrg = 'embulk-output-sftp'
        repo = 'maven'
        name = project.name
        desc = 'SFTP output plugin for Embulk'
        websiteUrl = 'https://github.com/embulk/embulk-output-sftp'
        issueTrackerUrl = 'https://github.com/embulk/embulk-output-sftp/issues'
        vcsUrl = 'https://github.com/embulk/embulk-output-sftp.git'
        licenses = ['MIT']
        labels = ['embulk', 'java']
        publicDownloadNumbers = true

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        embulkMavenRelease(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationTo???Repository".
            from components.java
            artifact testsJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    authors = [ "Civitaspo", "Satoshi Akama" ]
    email = [ "civitaspo@gmail.com", "satoshiakama@gmail.com" ]
    summary = "SFTP file output plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-output-sftp"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}
